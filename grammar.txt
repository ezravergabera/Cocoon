The root has the least priority. bottom has the highest priority.
root            : statements

statements      : NEWLINE* expr  NEWLINE* SEMICOLON ( NEWLINE* expr NEWLINE* SEMICOLON)*

expr            : num-assign
                : DTYPE:(deci | decimal) ID ASSIGN (expr | KWORD:get)
                : DTYPE:(bool | boolean) ID ASSIGN (BOOL:(true | false) | KWORD:get)
                : DTYPE:(char | character) ID ASSIGN (CHAR | KWORD:get)
                : DTYPE:text ID ASSIGN (STR | KWORD:get)
                : rel-expr ((AND | OR) rel-expr)*

num-assign      : DTYPE:(num | number) ID ASSIGN (expr | KWORD:get)
                
rel-expr        : NOT rel-expr
                : arith-expr ((GREATER | LESS | GREATEREQUAL | LESSEQUAL | EQUALTO | NOTEQUAL) arith-expr)*

arith-expr      : term ((PLUS | MINUS) term)*

term            : factor ((MUL | DIV | INTDIV | MOD) factor)*

factor          : (PLUS|MINUS) factor
                : power

power           : call (EXPO factor)*

call            : atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom            : INT | FLOAT | (ID | RWORD: empty | KWORD: show | KWORD: get) | BOOL | STR | CHAR
                : LPAREN expr RPAREN
                : list-expr
                : ask-expr
                : repeat-expr
                : while-expr
                : build-def

ID              : ID (incre-expr|assign-expr)?

list-expr       : LSQUARE (expr (COMMA expr)*)? RSQUARE

ask-expr        : RWORD:ask LPAREN expr RPAREN (NWORD:do)? LSQUARE
                  (expr SEMICOLON (RSQUARE | statements RSQUARE) askmore-expr|more-expr|SEMICOLON)
                | NEWLINE statements RSQUARE (askmore-expr|more-expr|SEMICOLON)

askmore-expr    : RWORD:askmore LPAREN expr RPAREN (NWORD:do)? LSQUARE
                  (expr SEMICOLON (RSQUARE | statements RSQUARE) askmore-expr|more-expr|SEMICOLON)
                | NEWLINE statements RSQUARE (askmore-expr|more-expr|SEMICOLON)

more-expr       : RWORD:more LSQUARE 
                  expr SEMICOLON (RSQUARE | statements RSQUARE)
                | NEWLINE statements RSQUARE

repeat-expr     : RWORD:repeat LPAREN num-assign; var_name; expr RPAREN LSQUARE
                  expr SEMICOLON (RSQUARE | statements RSQUARE)
                | (NEWLINE statements RSQUARE)

while-expr      : RWORD:while LPAREN expr RPAREN (NWORD:do)? LSQUARE
                  expr SEMICOLON (RSQUARE | statements RSQUARE)
                | (NEWLINE statements RSQUARE)

incre-expr      : ID PLUS PLUS | ID MINUS MINUS

assign-expr     : ID ASSIGN (expr | ask-expr)

build-def       : RWORD:build ID?
                  LPAREN (ID (COMMA ID)*)? RPAREN
                  (LSQUARE expr)
                | (NEWLINE statements RSQUARE)